//evaluationDependsOn(':jebu-api')

allprojects {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'eclipse'
	apply plugin: 'jacoco'
	
	group = 'de.nikem.jebu'
	version = '0.1.0-SNAPSHOT'
	
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	dependencies {
		compile 'org.slf4j:slf4j-api:1.7.20'
		testCompile 'junit:junit:4.+'
		testCompile 'org.mockito:mockito-core:1.10.19'
		testCompile 'org.slf4j:slf4j-simple:1.7.20'
	}
	
	repositories {
		mavenCentral()
	}
	
	test {
		systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'trace'
		testLogging.showStandardStreams = true
	}
	
	jacocoTestReport {
		reports {
			xml.enabled = true
			html.enabled = true
		}
	}
	
	check.dependsOn jacocoTestReport
	
	[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'
	
	eclipse {
		classpath {
			containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
			containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-' + sourceCompatibility
		}
	}
	
	eclipseJdt << {
		File f = file('.settings/org.eclipse.core.resources.prefs')
		f.write('eclipse.preferences.version=1\n')
		f.append('encoding/<project>=UTF-8')
	}
	
	sourceSets.main.java.srcDirs += 'src/main/webapp'
}



jar {
	manifest {
		attributes 'Implementation-Title': 'Java Event BUs',
		'Implementation-Version': version
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}

task printGradleVersion << {
	// Get current Gradle version as object.
	final GradleVersion gradleVersion = GradleVersion.current()
	 
	// Print different properties.
	println "Your Gradle version is ${gradleVersion.version}"
	println "Base version: ${gradleVersion.baseVersion}"
	println "Build time  : ${gradleVersion.buildTime}"
	println "Build number: ${gradleVersion.buildNumber}"
	println "Commit id   : ${gradleVersion.revision}"
	println "Next major  : ${gradleVersion.nextMajor}"
	println "Snapshot?   : ${gradleVersion.snapshot}"
}

eclipseJdt.dependsOn printGradleVersion