plugins {
	id "com.bmuschko.nexus" version "2.3.1"
}

description = 'Java Event BUs - a publish subscriber implementation for Java'
allprojects {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'eclipse'
	apply plugin: 'jacoco'
	apply plugin: 'signing'
	apply plugin: 'com.bmuschko.nexus'
	
	group = 'de.nikem.jebu'
	version = '0.4.0'
	def myName = name
	
	sourceCompatibility = '1.6'
	targetCompatibility = '1.6'
	
	sourceSets.main.resources.srcDirs += 'src/main/webapp'


	modifyPom {
		project {
			name = myName
			description = description
			url 'https://github.com/rasenderhase/jebu'

			licenses {
				license {
					name = 'The Apache Software License, Version 2.0'
					url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution = 'repo'
				}
			}
			developers {
				developer {
					name = 'Andreas Knees'
					email = 'andreas.knees@nikem.de'
					organization = 'nikem GmbH'
					organizationUrl = 'http://www.nikem.de'
				}
			}
			scm {
				connection 'scm:git:git@github.com/rasenderhase/jebu.git'
				developerConnection 'scm:git:git@github.com/rasenderhase/jebu.git'
				url 'scm:git:git@github.com/rasenderhase/jebu.git'
			}
		}
	}

//	nexus {
//		sign = true
//		repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
//		snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
//	}

	dependencies {
		compile 'org.slf4j:slf4j-api:1.7.20'
		testCompile 'junit:junit:4.12'
		testCompile 'org.mockito:mockito-core:1.10.19'
		testRuntime 'org.slf4j:slf4j-simple:1.7.20'
	}
	
	repositories {
		mavenCentral()
		jcenter()
	}
	
	test {
		systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'trace'
		testLogging.showStandardStreams = true
	}
	
	jacocoTestReport {
		reports {
			xml.enabled = true
			html.enabled = true
		}
	}
	
	check.dependsOn jacocoTestReport
		
	[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'
	
	eclipse {
		classpath {
			containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
			containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-' + sourceCompatibility
		}
	}
	
	eclipseJdt << {
		File f = file('.settings/org.eclipse.core.resources.prefs')
		f.write('eclipse.preferences.version=1\n')
		f.append('encoding/<project>=UTF-8')
	}
	
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Java Event BUs',
		'Implementation-Version': version
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}

task printGradleVersion << {
	// Get current Gradle version as object.
	final GradleVersion gradleVersion = GradleVersion.current()
	 
	println "Your Gradle version is ${gradleVersion.version}"
	println "Base version: ${gradleVersion.baseVersion}"
	println "Build time  : ${gradleVersion.buildTime}"
	println "Build number: ${gradleVersion.buildNumber}"
	println "Commit id   : ${gradleVersion.revision}"
	println "Next major  : ${gradleVersion.nextMajor}"
	println "Snapshot?   : ${gradleVersion.snapshot}"
	println "OSSRH_USERNAME" : System.properties['signing.keyId']
}

eclipseJdt.dependsOn printGradleVersion